
@GenModel(
	codeFormatting="true", 
	editDirectory="/se.eskilson.egeo.model.edit/src", 
	editorDirectory="/se.eskilson.egeo.model.editor/src",
	testsDirectory="/se.eskilson.egeo.model.tests/src"
	)
	
package se.eskilson.egeo

import java.text.SimpleDateFormat
import java.text.ParseException

class CacheDB
{
	contains Cache[] caches
	contains Attribute[] attributes
	contains User[] users
}

type Date wraps java.util.Date
create
{
	try {
		if (it != null) {
			new SimpleDateFormat("yyyy-MM-dd").parse(it);
		}
	} catch (ParseException e) {
		throw new RuntimeException(e)
	}
}
convert
{
	if (it != null) {
		new SimpleDateFormat("yyyy-MM-dd").format(it)
	}
}

enum Container
{
	Micro,
	Small,
	Regular,
	Large
}

enum CacheType
{
	Traditional,
	Unknown,
	Multi,
	Event
}

enum LogType
{
	FoundIt,
	DidNotFind,
	NeedsMaintenance
}

class Attribute
{
	int attrId
	String name
}

class Description
{
	String text
	boolean html
}

class Coordinate
{
	double latitude
	double longitude
}

class User
{
	int userId
	String name
}

@GenModel(labelFeature="description")
class Cache
{
	String code
	int ident
	boolean available
	boolean archived
	String name
	String description
	Date time
	CacheType cacheType
	String placedBy
	String url
	refers User ownedBy
	Container cont
	refers Attribute[] attributes
	int difficulty
	int terrain
	String country
	String state
	contains Description shortDescription
	contains Description longDescription
	contains Coordinate coordinate
	contains Log[] logs
	String hint
}

class Log
{
	String text
	refers User user
	boolean encoded
	int logId
	Date date
	LogType logType
}
